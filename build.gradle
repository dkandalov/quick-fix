import static org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_1_8

buildscript {
	ext.kotlin_version = "1.8.21"
	repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
}
plugins {
	id "org.jetbrains.intellij" version "1.13.3"
	id "org.jetbrains.kotlin.jvm" version "1.8.21"
}
apply plugin:"java"
apply plugin:"kotlin"
apply plugin:"idea"
apply plugin:"org.jetbrains.intellij"

repositories {
	mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2' // also includes hamcrest-core
}

sourceSets {
	main {
		kotlin.srcDir "src"
		resources.srcDir "resources"
	}
	test {
		kotlin.srcDir "test"
	}
}

kotlin {
	jvmToolchain(17)
}
compileKotlin {
    // See also https://plugins.jetbrains.com/docs/intellij/using-kotlin.html#kotlin-standard-library
	compilerOptions {
		apiVersion.set(KOTLIN_1_8)
		languageVersion.set(KOTLIN_1_8)
		// Compiler flag to allow building against pre-released versions of Kotlin
		// because IJ EAP can be built using pre-released Kotlin but it's still worth doing to check API compatibility
		freeCompilerArgs = ["-Xskip-metadata-version-check"]
	}
}

intellij {
	// To find available IDE versions see https://www.jetbrains.com/intellij-repository/releases
	// and https://www.jetbrains.com/intellij-repository/snapshots
	version = System.getenv().getOrDefault("IJ_VERSION",
//			"222.3345.118", // Version which contains ActionUpdateThread
			"LATEST-EAP-SNAPSHOT"
	)
	downloadSources = true
	sameSinceUntilBuild = false
	updateSinceUntilBuild = false
}
