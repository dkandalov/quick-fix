import static org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_1_8

plugins {
	id "org.jetbrains.intellij" version "1.16.0"
	id "org.jetbrains.kotlin.jvm" version "1.8.21"
}

repositories {
	mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2' // also includes hamcrest-core
}

sourceSets {
	main {
		kotlin.srcDir "src"
		resources.srcDir "resources"
	}
	test {
		kotlin.srcDir "test"
	}
}

kotlin {
	jvmToolchain(17)
}
compileKotlin {
	compilerOptions {
		apiVersion.set(KOTLIN_1_8)
		languageVersion.set(KOTLIN_1_8)
		// Compiler flag to allow building against pre-released versions of Kotlin
		// because IJ EAP can be built using pre-released Kotlin but it's still worth doing to check API compatibility
		freeCompilerArgs = ["-Xskip-metadata-version-check"]
	}
}

intellij {
	// To find available IDE versions see https://www.jetbrains.com/intellij-repository/releases
	// and https://www.jetbrains.com/intellij-repository/snapshots
	version = System.getenv().getOrDefault("IJ_VERSION",
			"233.11799.241", // Version after EAP which has ShowIntentionActionsHandler.calcCachedIntentions()
//			"LATEST-EAP-SNAPSHOT"
	)
	downloadSources = true
	sameSinceUntilBuild = false
	updateSinceUntilBuild = false
}
